plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'io.github'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.railroad'
    mainClass = 'io.github.railroad.Startup'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.swing']
}

dependencies {
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'org.json:json:20240303'
    implementation 'io.github.palexdev:materialfx:11.17.0'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.apache.groovy:groovy-all:5.0.0-alpha-8'
    implementation 'org.gradle:gradle-tooling-api:7.3-20210825160000+0000'
    compileOnly group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.11.2'

    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    compileOnly 'org.projectlombok:lombok:1.18.32'
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Railroad",
                "Implementation-Version": "1.0-alpha",
                "Main-Class": "io.github.railroad.Startup"
        )
    }
}

shadowJar {
    zip64 true
    mergeServiceFiles()
}